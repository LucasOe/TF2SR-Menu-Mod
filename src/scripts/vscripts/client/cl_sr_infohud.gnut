globalize_all_functions

global struct InfoDisplay
{
    var infoTitle
}


int displayLines = 3 // maximum number of lines that can be displayed

struct
{
    array<InfoDisplay> infoDisplays
    table<string, string> infoNames = {}
    array<string> displayInfos = []
} file

void function SRInfoHUD_Init()
{
    RegisterInfo("sv_cheats")
    RegisterInfo("host_timescale")
    RegisterInfo("player_respawnInputDebounceDuration")

    for (int i = 0; i < displayLines; i++)
    {
        file.infoDisplays.append(CreateInfoDisplay(i))
    }
    thread InfoHud_Thread()
}

void function InfoHud_Thread()
{
    while (true)
    {
        WaitFrame()
        // check for TAS mode & clear info display
        if (GetConVarInt("voice_forcemicrecord") > 0)
        {
            for (int i = 1; i < displayLines; i++) {
                SetInfoName(file.infoDisplays[0], "TAS")
                SetInfoName(file.infoDisplays[i], "")
            }
        }
        else 
        {
            array<string> moddedVars = GetModdedVars()
            int slot = 0
            foreach(string m in moddedVars){
                if (!file.displayInfos.contains(m) || slot >= displayLines) {
                    continue;
                }
                // display names and values of modded ConVars
                SetInfoName(file.infoDisplays[slot], file.infoNames[m] + " " + GetConVarFloat(file.infoNames[m]).tostring())
                slot++
            }
            for (int i = displayLines - 1; i >= slot; i--)
            {
                SetInfoName(file.infoDisplays[i], "")
            }
        }    
    }
}

void function SetInfoName(InfoDisplay display, string name)
{
    RuiSetString( display.infoTitle, "msgText", name )
}

InfoDisplay function CreateInfoDisplay(int line)
{
    InfoDisplay display
    var rui
    rui = RuiCreate( $"ui/cockpit_console_text_top_left.rpak", clGlobal.topoCockpitHudPermanent, RUI_DRAW_COCKPIT, 0 )
    RuiSetInt( rui, "maxLines", 1 )
    RuiSetInt( rui, "lineNum", 1 )
    RuiSetFloat2( rui, "msgPos", <0.0, 0.05 * line, 0.0> )
    RuiSetString( rui, "msgText", "" )
    RuiSetFloat( rui, "msgFontSize", 40.0 )
    RuiSetFloat( rui, "msgAlpha", 0.7 )
    RuiSetFloat( rui, "thicken", 0.0 )
    RuiSetFloat3( rui, "msgColor", <1.0, 1.0, 1.0> )
    display.infoTitle = rui

    return display
}

void function RegisterInfo( string infoName )
{
    file.infoNames[infoName] <- infoName
    file.displayInfos.append(infoName)
}

array<string> function GetModdedVars()
{
    array<string> moddedVars = []

    if (GetConVarInt("sv_cheats") > 0) moddedVars.append("sv_cheats")
    if (GetConVarFloat("host_timescale") != 1) moddedVars.append("host_timescale")
    if (GetConVarFloat("player_respawnInputDebounceDuration") != 0.5) moddedVars.append("player_respawnInputDebounceDuration")

    return moddedVars
}